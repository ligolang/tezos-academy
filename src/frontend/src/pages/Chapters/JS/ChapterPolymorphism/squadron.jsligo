#include "central_types.jsligo"
#include "squadron_types.jsligo"

const moduleRequest = (moduleName: string, key: string, callbackAddress: address, m: modules) : [list<operation>, modules] => {
    const addr_opt: option<address> = Map.find_opt(moduleName, m);
    const addr: address = match(addr_opt) {
        when(Some(a)): a;
        when(None): failwith("Service not registered: " + moduleName) as address
    };
    const proposed_destination: contract<actionCentral> = Tezos.get_contract_with_error(addr, "Failed do get contract");
    const actRetrieve = {
        sKey: key,
        callbackAddress: Tezos.get_self_address
    };
    const proposedTransaction: operation = Tezos.transaction(RetrieveShip(actRetrieve), 0 as mutez, proposed_destination);
    const listoperation: list<operation> = list([proposedTransaction]);
    return [listoperation, m];
};

const moduleResponse = (e: ship, m: modules) : [list<operation>, modules] => {
    const enemy: bool = e.hostile;
    if (enemy) {
        return failwith("Fire !") as [list<operation>, modules]
    } else {
        failwith("Patrol !") as [list<operation>, modules]
    }
    return [list([]) as list<operation>, m];
};

const registerModule = (moduleName: string, moduleAddress: address, m: modules): [list<operation>, modules] => {
    const checkModule: option<address> = Map.find_opt(moduleName, m);
    const newModules: modules = match(checkModule) {
        when(Some(e)): failwith("Service already registered");
        when(None): Map.add(moduleName, moduleAddress, m)
    };
    return [list([]) as list<operation>, newModules];
};

const squadron = (action: actionSquadron, md: modules) : [list<operation>, modules] => match(action) {
  when(RegisterModule(m)): registerModule(m.name, m.moduleAddress, md);
  when(ModuleRequest(mr)): moduleRequest(mr.moduleName, mr.key, mr.callbackAddress, md);
  when(ModuleResponse(e)): moduleResponse(e.e, md)
};
